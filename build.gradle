apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'idea'
apply plugin: 'eclipse'
sourceCompatibility = '1.7'

def confDirName = 'conf'
distsDirName = 'dist'
libsDirName = 'lib'
dependencyCacheDirName = 'tmp'

def loadProperties(String sourceFileName) {
	def config = new Properties()
	def propFile = new File(sourceFileName)
	if (propFile.canRead()) {
		config.load(new FileInputStream(propFile))
		for (Map.Entry property in config) {
			//noinspection GroovyAssignabilityCheck
			ext[property.key] = property.value;
		}
	}
}

loadProperties confDirName + '/properties/run'

version = ext.version

configurations {
	//noinspection GroovyAssignabilityCheck
	apacheCommonsConfiguration {
		transitive = true
	}
	//noinspection GroovyAssignabilityCheck
	compile {
		extendsFrom apacheCommonsConfiguration
		transitive = true
	}
	//noinspection GroovyAssignabilityCheck
	runtime {
		extendsFrom compile
	}
}

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
dependencies {

	apacheCommonsConfiguration(
		'commons-configuration:commons-configuration:1.10'
	)

	compile (
		//
		'com.codahale.metrics:metrics-core:3.0.2',
		//
		'commons-codec:commons-codec:1.9',
		//
		'org.apache.logging.log4j:log4j-api:2.0.2',
		'org.apache.logging.log4j:log4j-core:2.0.2',
		//
		'org.apache.httpcomponents:httpclient:4.3.4',
		'org.apache.httpcomponents:httpcore:4.3.2',
		'org.eclipse.jetty:jetty-server:9.2.3.v20140905',
		'org.eclipse.jetty:jetty-webapp:9.2.3.v20140905',
		'javax.el:javax.el-api:3.0.1-b04',
		'javax.servlet:javax.servlet-api:3.1.0',
		'org.mortbay.jetty:jsp-2.1-glassfish:2.1.v20100127',
		'jstl:jstl:1.2',
		'org.eclipse.jetty.websocket:websocket-server:9.2.3.v20140905',
		'org.eclipse.jetty.websocket:websocket-core:9.0.0.M2',
		'com.google.code.gson:gson:2.3'
	)
	runtime (
		//
		'com.fasterxml.jackson.core:jackson-annotations:2.4.1',
		'com.fasterxml.jackson.core:jackson-core:2.4.1',
		'com.fasterxml.jackson.core:jackson-databind:2.4.1',
		//
		'com.github.rickyclarkson:jmdns:3.4.2-r353-1',
		//
		'commons-lang:commons-lang:2.6',
		'commons-logging:commons-logging:1.1.3',
		//
		'org.apache.logging.log4j:log4j-slf4j-impl:2.0.2',
		'org.apache.logging.log4j:log4j-jcl:2.0.2',
		'org.slf4j:slf4j-api:1.7.7',
		//
		'org.python:jython:2.7-b2',
		'org.postgresql:postgresql:9.3-1102-jdbc3',
		'org.fusesource.jansi:jansi:1.11'
	)


	testCompile group: 'junit', name: 'junit', version: '4.11'
}

task rmic(dependsOn: compileJava) << {
	String[] listClsRemote = [
		'com.emc.mongoose.web.load.server.impl.BasicLoadBuilderSvc',
		'com.emc.mongoose.web.load.server.impl.type.AppendSvc',
		'com.emc.mongoose.web.load.server.impl.type.CreateSvc',
		'com.emc.mongoose.web.load.server.impl.type.DeleteSvc',
		'com.emc.mongoose.web.load.server.impl.type.ReadSvc',
		'com.emc.mongoose.web.load.server.impl.type.UpdateSvc'
	]
	String clsPath = """${sourceSets.main.output.classesDir}:${configurations.apacheCommonsConfiguration.asPath}"""
	for(cls in listClsRemote) {
		String command = """rmic -classpath ${clsPath} -d ${sourceSets.main.output.classesDir} ${cls}"""
		//println command
		String test = command.execute().text
		print test
	}
}

//noinspection GroovyAssignabilityCheck
jar {
	dependsOn(rmic)
	archiveName "${baseName}.${extension}"
	def clsPath = configurations.runtime.collect { 'lib/'+it.name }.join(' ')
	manifest {
		attributes (
			'Implementation-Title': rootProject.name,
			'Implementation-Version': version,
			'Main-Class': 'com.emc.mongoose.run.Main',
			'Class-Path': clsPath
		)
	}
}

distributions {
	//noinspection GroovyAssignabilityCheck
	main {
		baseName = rootProject.name
		contents {
			into('conf') {
				from { 'conf' }
			}
			into('lib') {
				from { libsDirName }
			}
			into('scripts') {
				from { sourceSets.test.resources }
			}
			into('webapp') {
				from { 'src/main/webapp' }
			}
			into('webapp/WEB-INF/lib') {
				from { configurations.compile.files {dep -> dep.name == 'jstl'} }
			}
		}
	}
}

idea {
	module {
		name = rootProject.name
		jdkName = sourceCompatibility
	}
	project.ipr {
		withXml { provider ->
			provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
		}
	}
}

tasks.idea.dependsOn(cleanIdea)
